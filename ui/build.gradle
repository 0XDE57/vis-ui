apply plugin: "java"
apply plugin: 'maven'
apply from: '../deploy.gradle'

group = 'com.kotcrab.vis'
version = '1.0.2-SNAPSHOT'
archivesBaseName = "vis-ui"
sourceCompatibility = 1.6
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.test.java.srcDirs = [ "test/" ]

test {
    useJUnit {
        include "**/*Test.**"
        exclude 'com.kotcrab.vis.ui.test.manual.**'
    }
}

processResources {
    from ('src/') {
        exclude ('**/*.java')
    }
}

eclipse.project {
    name = appName + "-ui"
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name 'vis-ui'
                artifactId 'vis-ui'
                packaging 'jar'
                description 'UI toolkit and flat design skin for scene2d.ui'
            }
        }
    }
}

task run(dependsOn: jar, type: JavaExec ) {
    main = 'com.kotcrab.vis.ui.test.manual.TestLauncher'
    classpath = sourceSets.test.runtimeClasspath
    ignoreExitValue = true
}
