apply plugin: 'java'
apply plugin: 'maven'
apply from: '../deploy.gradle'

group = 'com.kotcrab.vis'
version = '0.3.2-SNAPSHOT'
archivesBaseName = "vis-editor"
sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
        resources {
            srcDir 'assets/'
        }
    }
}

eclipse.project {
    name = appName + "-editor"
}

processResources {
    from('src/') {
        exclude('**/*.java')
    }
}

jar {
    manifest {
        attributes(
                "Build-Timestamp": buildTimestamp,
                "Class-Path": configurations.runtime.files.collect { "lib/" + it.name }.join(' '),
                "Build-Jdk": JavaVersion.current(),
                "Main-Class": mainClassName,
        )
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name 'vis-editor'
                artifactId 'vis-editor'
                packaging 'jar'
                description 'Scene Editor for LibGDX'
            }
        }
    }
}

task copyPlugins (dependsOn: [':plugins:vis-editor-spine:jar', ':plugins:vis-editor-spine-notifier:jar']) {
    def spine = project(':plugins:vis-editor-spine')
    def spineRuntime = project(':plugins:vis-runtime-spine')
    def spineNotifier = project(':plugins:vis-editor-spine-notifier')

    doLast {
        copy {
            from spine.jar
            into sourceSets.main.output.classesDir.path + '/plugins/spine'
        }

        copy {
            from spineRuntime.jar
            into sourceSets.main.output.classesDir.path + '/plugins/spine/lib'
        }

        copy {
            from spineNotifier.jar
            into sourceSets.main.output.classesDir.path + '/plugins/spine-notifier'
        }
    }
}


task run(dependsOn: copyPlugins, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    ignoreExitValue = true
}
